
{
    var indentStack = [], indent = "";

    function Noop() {
        this.nodeType = 'Noop';
    }

    function Number(digits) {
        this.nodeType = 'Number';
        this.value = parseInt(digits.join(''));
    }

    function Expression (children) {
        this.nodeType = 'Expression'
        this.children = children;
    }

    function Declaration (name, value) {
        this.nodeType = 'Declaration';
        this.name = name;
        this.value = value;
    }

    function Condition (expr, ifBody, elseBody) {
        this.nodeType = 'Condition';
        this.expression = expr;
        this.ifBody = ifBody;
        this.elseBody = elseBody;
    }

    function ForLoop (iterator, item, iterable, body) {
        this.nodeType = 'For';
        this.iterator = iterator;
        this.item = item;
        this.iterable = iterable;
        this.body = body;
    }

    function WhileLoop (expression, body) {
        this.nodeType = 'WhileLoop';
        this.expression = expression;
        this.body = body;
    }

    function Operator (op, left, right) {
        this.nodeType = 'Operator';
        this.operator = op;
        this.left = left;
        this.right = right;
    }

}

program
    = statement*

statement 
    = SAMEDENT? EOL
      { return new Noop(); }

    / SAMEDENT a:decl EOL
      { return a; }

    / SAMEDENT a:expr EOL
      { return a; }

    / SAMEDENT 'if ' a:expr EOL INDENT ifBody:statement+ DEDENT
      SAMEDENT 'else' EOL INDENT elseBody:statement+ DEDENT 
      { return new Condition(a, ifBody, elseBody); }

    / SAMEDENT 'if ' a:expr EOL INDENT ifBody:statement+ DEDENT
      { return new Condition(a, ifBody); }

    / SAMEDENT 'for ' iterator:ident ', ' item:ident ' in ' iterable:expr EOL INDENT body:statement+ DEDENT
      { return new ForLoop(iterator, item, iterable, body); } 

    / SAMEDENT 'for ' item:ident ' in ' iterable:expr EOL INDENT body:statement+ DEDENT
      { return new ForLoop(null, item, iterable, body); } 

    / SAMEDENT 'while ' e:expr EOL INDENT body:statement+ DEDENT
      { return new WhileLoop(e, body); } 

EOL
    = '\r\n' / '\n' / '\r'

SAMEDENT
    = i:[ \t]* &{ return i.join("") === indent; }

INDENT
    = i:[ \t]+ &{ return i.length > indent.length; }
    { indentStack.push(indent); indent = i.join(""); pos = offset; }

DEDENT
    = { indent = indentStack.pop(); }

decl 
    = a:ident _ '=' _ b:expr { return new Declaration(a, b) }

expr 
    = left:term _ op:[+-] _ right:expr { return new Operator(op, left, right); }
    / term

term
    = left:value _ op:[*/] _ right:term { return new Operator(op, left, right); }
    / value

value
    = number
    / a:ident { return new Expression([a]); }
    / '(' _ a:expr _ ')' { return a; }

number 
    = digits:[0-9]+ { return new Number(digits); }

ident
    = a:(digits / letters / '_')+ { return a.join(''); }

digits
    = a:[0-9]+ { return a.join(''); }

letters
    = a:[a-zA-Z]+ { return a.join(''); }

_
    = [ \t]*